plugins {
    id 'java'
    id 'maven-publish'
}

group = 'info.logbat'
version = '0.1.1'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "local-repo"
            url = project.layout.buildDirectory.dir("java").get().asFile.toURI()
        }
    }
}

dependencies {
    // Logback Classic
    implementation 'ch.qos.logback:logback-classic:1.5.7'
    // Slf4j API
    implementation 'org.slf4j:slf4j-api:2.0.16'
    // Jackson ObjectMapper
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
    // SnakeYAML
    implementation 'org.yaml:snakeyaml:2.2'

    // Junit5
    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.0'
    // AssertJ
    testImplementation 'org.assertj:assertj-core:3.26.3'
    // Jackson JSR310
    testImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.17.2'
    // Mockito
    testImplementation 'org.mockito:mockito-core:5.12.0'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes(
                'Implementation-Title': 'Logbat SDK',
                'Implementation-Version': version
        )
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE // 중복된 파일 무시

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}